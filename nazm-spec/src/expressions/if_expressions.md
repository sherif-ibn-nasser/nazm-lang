## تعبيرات 'لو'

تُستخدم تعبيرات `لو` في لغة **نظم** لتنفيذ مجموعة من الجُمَل بناءً على تحقق شرط معين. إذا كان الشرط صحيحًا، يتم تنفيذ الجمل داخل نِطاق `لو`. بالإضافة إلى ذلك، يمكن استخدام تعبير `وإلا` لتحديد مجموعة جُمَل تُنفذ إذا لم يتحقق الشرط. يمكن أيضاً دمج عدة تعبيرات `لو` باستخدام `وإلا لو` لتحديد حالات مختلفة متعددة.

```nazm
دالة البداية() {
    احجز درجة_الحرارة = 85؛

    احجز حالة_المياه = لو درجة_الحرارة >= 100 {
        // لاحظ عدم وجود فاصلة منقوطة لأننا نريد إرجاع القيمة من ذلك التعبير
        "غليان"
    } وإلا لو درجة_الحرارة >= 0 {
        "سائلة"
    } وإلا {
        "متجمدة"
    }؛

    اظهر_(حالة_المياه)؛

    // هذا التعبير خاطئ
    // ﻷنه يجب أن يتم تحديد القيمة التي سترجع إذا لم يتحقق الشرط
    // أي يجب إضافة طرف `وإلا`
    // احجز حالة_المياه2 = لو درجة_الحرارة >= 100  { "غليان" }؛

    // وكذلك هنا يجب إضافة طرف `وإلا` في حالة عدم تحقق أي شرط
    // احجز حالة_المياه3 = لو درجة_الحرارة >= 100 {
    //     "غليان"
    // } وإلا لو درجة_الحرارة >= 0 {
    //     "سائلة"
    // }؛

    // هذا التعبير خاطئ
    // ﻷن القيمتين التي سترجع من طرف `لو` وطرف `وإلا` يجب أن يكونا من نفس النوع
    // احجز حالة_المياه4 = لو درجة_الحرارة < 0 { "المياه متجمدة" } وإلا { 50 }؛

}
```

من الممكن أيضاً استخدام تعبيرات `لو` كجُمَل كالتالي:

```nazm
دالة البداية() {
    احجز درجة_الحرارة = 85؛

    لو درجة_الحرارة >= 100 {
        اظهر_("المياه في حالة غليان")؛
    } // استخدامها كجُملة لا يلزم مُعالجة إذا لم يتحقق الشرط
}
```

### النحو

> `تعبير_لو` :
>
> &emsp; '**`لو`**' &nbsp; [تعبير](../expressions.md) &nbsp; '**`{`**' &nbsp; [جُمَل]()<sup>؟</sup> &nbsp; '**`}`**'
>
> &emsp; ( '**`وإلا`**' &nbsp; '**`لو`**' &nbsp; '**`{`**' &nbsp; [جُمَل]()<sup>؟</sup> &nbsp; '**`}`**' )<sup>\*</sup>
>
> &emsp; ( '**`وإلا`**' &nbsp; '**`{`**' &nbsp; [جُمَل]()<sup>؟</sup> &nbsp; '**`}`**' )<sup>؟</sup>
