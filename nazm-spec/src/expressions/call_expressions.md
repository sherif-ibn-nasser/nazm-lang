## تعبيرات الاستدعاء

في لغة **نظم**، يتم استخدام تعبيرات الاستدعاء لتنفيذ الدوال أو الإجراءات. تعبير الاستدعاء يتضمن [تعبيراً برمجياً](../expressions.md)، متبوعًا بالأقواس العادية `()` التي تحتوي على المعاملات (إن وُجدت) التي يتم تمريرها إلى الدالة.

بعض الأمثلة على تعبيرات الاستدعاء:

```nazm
دالة اطرح (ن1: ط4، ن2: ط4): ط4 {
    ن1 - ن2
}

دالة البداية() {

    اطرح(3، 2)؛

    ملف_آخر::اجمع(3، 2)؛

    اظهر_("نظم".الحروف().الطول())؛ // 3
}
```

### تعبيرات اللامدا المُلحَقة

عند استدعاء [دالة عالية الرتبة]() ،آخر مُعاملاتها هى [تعبير لامدا](../expressions/lambda_expressions.md)، فإنه يمكن استدعاء الدالة وتمرير تعبير اللامدا خارج أقواس الدالة. إليك مثال لتعريف دالة تقوم بتكرار أي كود عدد معين من المرات وطريقة استدعائها باستخدام تعبير اللامدا المُلحَق:

```nazm
دالة تكرار_(ن: ط4، كود: () -> ()) {
    احجز متغير م = 0؛

    طالما م < ن {
        كود()؛ // استدعاء الكود المُراد تكراره
        م += 1؛
    }
}

دالة البداية() {

    تكرار_(5، {
        اظهر_("هذا تعبير لامدا")؛
    })؛ // بالطبع كثرة الأقواس تضايق

    // لنقم بإخراج تعبير اللامدا

    تكرار_(5) {
        اظهر_("هذا تعبير لامدا من الخارج")؛
    }؛ // بالطبع هكذا أفضل

}
```

إذا كانت الدالة لا يوجد لديها مُعاملات سوى تعبير اللامدا، فإنه يمكن إهمال الأقواس أيضاً:

```nazm
دالة نطاق(كود: () -> ()) {
    كود()؛ // استدعاء الكود المُراد تكراره
}

دالة البداية() {

    نطاق {
        احجز م = 5؛
    }

    // لا يمكن الوصول إلى م من هنا
}
```

تعبيرات اللامدا المُلحَقة -المُستوحاة من [كوتلن](https://kotlinlang.org/docs/lambdas.html#passing-trailing-lambdas)- تُمكَّنك من كتابة الكود بنمط البرمجة الوظيفية بشكل أجمل:

```nazm
دالة البداية() {

    احجز أعداد = [1، 2-، 3، 4، 5]؛

    احجز المجموع = أعداد.اختزال { مجموع، عدد -> مجموع + عدد }؛

    احجز الأعداد_سالبة = أعداد.صياغة { -س }؛

}
```

### النحو

> `تعبير_استدعاء` :
>
> &emsp; [تعبير](../expressions.md) &nbsp; ( <br> &emsp; &emsp; &emsp; '**`(`**' &nbsp; **`قيم_مُعاملات_الدالة`**<sup>؟</sup> &nbsp; '**`)`**' &nbsp; [تعبير_لامدا](../expressions/lambda_expressions.md#النحو)<sup>؟</sup> <br> &emsp; &emsp; \| &nbsp; [تعبير_لامدا](../expressions/lambda_expressions.md#النحو) <br> &emsp; )
>
> `قيم_مُعاملات_الدالة` :
>
> &emsp; [تعبير](../expressions.md) &nbsp; ( '**`،`**' &nbsp; [تعبير](../expressions.md) )<sup>\*</sup> &nbsp; **`،`**<sup>؟</sup>
