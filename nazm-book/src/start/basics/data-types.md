## فصل: أنواع البيانات

في لغة `نظم`، لكل قيمة نوع معين يمثل شكل البيانات التي يتم التعامل معها. معرفة أنواع البيانات تساعدك على استخدام القيم بشكل صحيح والتعامل مع البيانات بكفاءة. في هذا الفصل، سنتعرف على الأنواع الأساسية التي تدعمها اللغة.

### الأعداد الصحيحة

الأعداد الصحيحة في لغة `نظم` هي الأعداد التي لا تحتوي على كسور أو أرقام عشرية. يتم استخدام هذا النوع عند التعامل مع القيم العددية الصحيحة. على سبيل المثال:

```nazm
دالة البداية() {
   اظهر("عدد أسطر هذا البرنامج هو: ")؛
   اظهر_(4)؛
}
```

عند التنفيذ:

```shell, rtl
عدد أسطر هذا البرنامج هو: 4
```

يوجد في `نظم` 8 أنواع للقيم العددية الصحيحة، لكل نوع حجم محدد في الذاكرة وقيمة صُغرى وقيمة عُليا، فباختلاف الحجم تختلف القيم القُصوى للنوع.
يمكن تقسيم تلك الأنواع إلى قسمين (كما في علوم الرياضيات):

- **مجموعة الأعداد الطبيعية**
  وهى الأعداد التي تتكون من **الصفر** مع الأعداد **الموجبة**، فتكون القيمة الصُغرى هى صفر والقيمة العُليا تعتمد على **حجم النوع** نفسه.

- **مجموعة الأعداد الصحيحة**
  وهى الأعداد التي تضم الصفر والأعداد الموجبة وأيضاً الأعداد **السالبة**، والقيمة الصُغرى والقيمة العُليا تعتمدان على **حجم النوع** نفسه.

|  النوع   | الحجم  | المدى                                                                   |
| :------: | :----: | ----------------------------------------------------------------------- |
| **`ط1`** | 1 بايت | من **`0`** إلى **`255`**                                                |
| **`ط2`** | 2 بايت | من **`0`** إلى **`65,535`**                                             |
| **`ط4`** | 4 بايت | من **`0`** إلى **`4,294,967,295`**                                      |
| **`ط8`** | 8 بايت | من **`0`** إلى **`18,446,744,073,709,551,615`**                         |
| **`ص1`** | 1 بايت | من **`-128`** إلى **`127`**                                             |
| **`ص2`** | 2 بايت | من **`-32,768`** إلى **`32,767`**                                       |
| **`ص4`** | 4 بايت | من **`-2,147,483,648`** إلى **`2,147,483,647`**                         |
| **`ص8`** | 8 بايت | من **`-9,223,372,036,854,775,808`** إلى **`9,223,372,036,854,775,807`** |

نلاحظ من الجدول أن أنواع الأعداد الطبيعية تبدأ بالحرف **`ط`** ثم حجم النوع، وكذلك أنواع الأعداد الصحيحة تبدأ بالحرف **`ص`** ثم حجم النوع.

> [!ملحوظة]
> البايت هو أصغر وحدة تخزين في الذاكرة، وهو يتكون من وحدات أصغر تُسمى البت، والبايت الواحد يتكون من 8 بت، فيكون النوع الذي حجمه 8 بايت يتكون من 64 بت. سوف نناقش هذا بتفصيلٍ أكثر إن شاء الله في جزء [الذاكرة]().

**اللواحق العددية**

يُمكن إلحاق نوع العدد في نهايته لتحديد نوعه بشكل صريح في الكود. إذا لم يتم إضافة لاحقة للعدد، فإن مترجم `نظم` سيقوم بتحديد النوع تلقائيًا بناءً على سياق الكود وعملية التحقق من الأنواع.

```nazm
دالة البداية() {

   اظهر("عدد بلاحقة ص2 (نوع العدد ص2): ")؛
   اظهر_(50ص2)؛

   اظهر("عدد بلاحقة ط8 (نوع العدد ط8): ")؛
   اظهر_(50ط8)؛

   اظهر("عدد بدون لاحقة (نوعه سيتم تحديده من قبل المترجم): ")؛
   اظهر_(50)؛

}
```

ستلاحظ أن اللواحق لا تؤثر على العدد المعروض عند التنفيذ، ولكنها فقط تؤثر على نوع وحجم العدد المستخدم داخل البرنامج، فهذا ما سيظهر عند تنفيذ الكود السابق:

```shell, rtl
عدد بلاحقة ص2 (نوع العدد ص2): 50
عدد بلاحقة ط (نوع العدد ط): 50
عدد بدون لاحقة (نوعه سيتم تحديده من قبل المترجم): 50
```

**الفاصلة**

يمكن استخدام الفاصلة (`,`) كفاصل بصري للأعداد الكبيرة لتسهيل قراءتها. يُستخدم هذا النمط عادةً لفصل الأرقام إلى مجموعات، مما يُسهل قراءة القيم الكبيرة. فيمكنك كتابة العدد `155656826` بالصور التالية في الكود:

```nazm
دالة البداية() {

   اظهر("هذا رقم كبير يسهل قراءته: ")؛
   اظهر_(155,656,826)؛

   اظهر("ويمكن كتابته هكذا أيضاً: ")؛
   اظهر_(1,5,5,6,5,6,8,2,6)؛

}
```

لاحظ أن تلك الفواصل غير مؤثرة على ما يظهر على شاشة التنفيذ:

```shell, rtl
هذا رقم كبير يسهل قراءته: 155656826
ويمكن كتابته هكذا أيضاً: 155656826
```

**أنظمة العد**

أنظمة العد هي طرق مختلفة لتمثيل الأعداد. في حياتنا اليومية، نستخدم **النظام العشري** الذي يعتمد على الأرقام من 0 إلى 9، لكنه ليس النظام الوحيد. في لغة البرمجة `نظم` -وأيضاً كما هو الحال في العديد من لغات البرمجة- ، يمكننا استخدام أنظمة عد أخرى حسب الحاجة:

- **النظام الثُنائي**: يعتمد على الرقمين 0 و 1 ويُستخدم في الإلكترونيات والحوسبة.
- **النظام الثُماني**: يعتمد على الأرقام من 0 إلى 7.
- **النظام السُداسي عشر**: يستخدم الأرقام من 0 إلى 9 والحروف من A إلى F.

لكتابة العدد بأي نظام، قُم بكتابة أولاً بادئة النظام (والتي تُمثل عدد أرقام النظام المتاحة، مثل النظام الثُماني نستعمل الرقم 8)، ثم العلامة `#`، ثم العدد الذي تريده.

**مثال**: هنا تمثيل للعدد `4537` بالأنظمة المختلفة:

```nazm
دالة البداية() {

   اظهر("العدد 1000110111001 في النظام الثُنائي يُمثِّل في النظام العشري العدد: ")؛
   اظهر_(2#1000110111001)؛

   اظهر("العدد 10671 في النظام الثُماني يُمثِّل في النظام العشري العدد: ")؛
   اظهر_(8#10671)؛

   اظهر("العدد 11B9 في النظام السُداسي عشر يُمثِّل في النظام العشري العدد: ")؛
   اظهر_(16#11B9)؛

   اظهر("العدد 4537 في النظام العشري يُمثِّل في النظام العشري العدد: ")؛
   اظهر_(4537)؛

}
```

ستُلاحظ عند التنفيذ أن تلك الأعداد ستظهر بالنظام العشري، وذلك لأنها فقط مجرد تمثيل لنفس العدد:

```shell, rtl
العدد 1000110111001 في النظام الثُنائي يُمثِّل في النظام العشري العدد: 4537
العدد 10671 في النظام الثُماني يُمثِّل في النظام العشري العدد: 4537
العدد 11B9 في النظام السُداسي عشر يُمثِّل في النظام العشري العدد: 4537
العدد 4537 في النظام العشري يُمثِّل في النظام العشري العدد: 4537
```

يُمكنك أيضاً استعمال الفواصل أو وضع لاحقة في نهاية العدد مع أي نظام عددي:

```nazm
   اظهر_(2#1,0001,1011,1001ص8)؛
```

> [!ملحوظة]
> إذا كنت ترغب في إظهار الأعداد بالنظام الثُنائي (أو أي نظام عددي آخر) على الشاشة،
> فهذا غير ممكن في الوقت الحالي نظرًا لأن اللغة لا تزال قيد التطوير.
> ولكن من المخطط، بإذن الله، إضافة دوال مستقبلاً لعرض الأعداد بمختلف أنظمة العد.

### الأعداد العشرية

الأعداد العشرية في لغة `نظم` هي الأعداد التي تحتوي على جزء عشري، أي أنها تمثل الأرقام التي تشمل كسورًا. تستخدم هذه الأعداد عند الحاجة للتعامل مع قيم غير صحيحة، مثل القياسات. على سبيل المثال:

```nazm
دالة البداية() {
   اظهر("قيمة الثابت الرياضي π: ")؛
   اظهر_(3.14159265359)؛
}
```

الأعداد العشرية في `نظم` يتم تمثيلها حسب معيار IEEE لحسابات النقطة العائمة (IEEE 754).

**الصيغة العلمية**

الصيغة العلمية تعبر عن العدد العشري بشكل مختصر، حيث يتم تمثيل العدد على شكل حاصل ضرب عدد بين 1 و 10 مضروبًا في 10 مرفوعًا لأس صحيح.

على سبيل المثال، العدد 123,000,000,000 يُكتب بالصورة العلمية: \\( 1.23 \times 10^{11} \\)

وبالمثل، يمكن تمثيل الأعداد الصغيرة جداً. فالعدد 0.000456 يُمكن كتابته بالصورة العلمية: \\( 4.56 \times 10^{-4} \\)

في لغة `نظم`، يُستخدم الرمز `^^` للدلالة على الأس:

```nazm
دالة البداية() {
   اظهر("123,000,000,000 بالصيغة العلمية: ")؛
   اظهر_(1.23^^11)؛

   اظهر("0.000456 بالصيغة العلمية: ")؛
   اظهر_(4.56^^-4)؛
}
```

سيظهر عند التنفيذ:

```shell, rtl
123,000,000,000 بالصيغة العلمية: 1.23^^11
0.000456 بالصيغة العلمية: 4.56^^-4
```

**الدقة**

تتفاوت دقة الأعداد العشرية بسبب كيفية تخزينها في الذاكرة. فعند التعامل مع الأعداد العشرية، يمكن أن تؤدي العمليات الحسابية المختلفة إلى نتائج تقريبية، مما يعني أن القيم المعروضة قد لا تكون دائمًا دقيقة بالكامل. لذا، من المهم مراعاة هذه الدقة عند استخدام الأعداد العشرية في البرنامج.

الأعداد العشرية في الواقع ليس لها حد معين، حيث يمكن أن تمتد نظريًا من `-∞` (سالب ما لا نهاية) إلى `+∞` (موجب ما لا نهاية). ومع ذلك، عندما يتعلق الأمر بتخزين هذه الأعداد في ذاكرة الكمبيوتر، فإن القيود المفروضة على حجم الذاكرة تجعل من المستحيل تمثيل جميع الأعداد الحقيقية بدقة.

على سبيل المثال، العدد `0.1` لا يمكن تمثيله بدقة كاملة داخل ذاكرة الكمبيوتر باستخدام النقطة العائمة. وبدلاً من تخزين القيمة الدقيقة `0.1`، يتم تخزين عدد قريب جدًا منها، مثل `0.100000001490116119384765625`. هذه الفجوة الدقيقة ناتجة عن طبيعة تمثيل النقطة العائمة، الذي يعتمد على عدد محدود من البتات لتخزين الأرقام العشرية. على الرغم من أن الفارق ضئيل للغاية، إلا أنه يمكن أن يؤدي إلى أخطاء تراكمية في العمليات الحسابية الكبيرة أو المعقدة. سوف نتعرف أكثر على كيفية يتم تخزين الأعداد العشرية بتفصيلٍ أكثر إن شاء الله في جزء [الذاكرة]().

> [!ملحوظة]
> عدم الدقة في تخزين الأعداد العشرية لا يعني أنها دائماً ستظهر غير دقيقة للمستخدم، فإنه قد تم تطوير خوارزميات خاصة **فقط** لعرض الأعداد العشرية في صورة تقريبية بنفس الدقة المتوقعة، فإنه عند عرض الرقم `0.1` فإنه سوف يظهر كما هو متوقع.

يوجد في `نظم` نوعان لتمثيل الأعداد العشرية، يتميز كل منهما بدقة تختلف باختلاف حجم النوع:

|  النوع   | الحجم  |   الدقة   |        أصغر قيمة دقيقة         |         أكبر قيمة دقيقة         |
| :------: | :----: | :-------: | :----------------------------: | :-----------------------------: |
| **`ع4`** | 4 بايت | 6-7 أرقام |     **`1.175494351^^-38`**     |     **`3.402823466^^+38`**      |
| **`ع8`** | 8 بايت | 15-16 رقم | **`2.2250738585072014^^-308`** | **` 1.7976931348623158^^+308`** |

> أصغر وأكبر قيمة دقيقة للنوع لا تمثل أكبر وأصغر قيمة مطلقاً للنوع،
> حيث يمكن أيضاً تمثيل الأعداد العشرية السالبة،
> بالإضافة إلى القيم الخاصة مثل `+∞` و`-∞`،
> وكذلك الحالات غير العددية (عدد غير مُعرَّف)، التي تنتج عن عمليات مثل محاولة حساب الجذر التربيعي لعدد سالب أو القسمة على صفر.

يمكنك بالطبع أيضاً إضافة لاحقة للعدد العشري أو استخدام الفواصل، كما فعلنا مع الأعداد الصحيحة.

### الشروط

في `نظم` -كما هو الحال في بقية لغات البرمجة- يوجد النوع الشَرطي (**شرط**)، الذي يمكن أن يأخذ قيمتين فقط: **`صحيح`** أو **`فاسد`**.
يُستخدم هذا النوع بشكل أساسي [للتحكم في التنفيذ]() تحت شروط معينة.

```nazm
دالة البداية() {
   اظهر("إذا كان الشرط متحققاً فإنه يمثل القيمة: ")؛
   اظهر_(صحيح)؛


   اظهر("أما إذا كان الشرط غير متحقق فإنه يمثل القيمة: ")؛
   اظهر_(فاسد)؛
}
```

```shell, rtl
إذا كان الشرط متحققاً فإنه يمثل القيمة: صحيح
أما إذا كان الشرط غير متحقق فإنه يمثل القيمة: فاسد
```

### الحروف

في `نظم`، يُمثل النوع (**حرف**) القيم النصية المكونة من حرف واحد فقط. هذا النوع يُستخدم بشكل رئيسي لتخزين الأحرف الفردية، مثل الأحرف الأبجدية أو الأرقام أو الرموز.

يتم تعريف الحرف بين علامتي تنصيص مُفرَدتين (`'`):

```nazm
دالة البداية() {
   اظهر_('أ')؛
   اظهر_('ب')؛
   اظهر_('ج')؛
   اظهر_('د')؛
   اظهر('!')؛
   اظهر(' ')؛
   اظهر('B')؛
   اظهر(' ')؛
   اظهر('_')؛
   اظهر_('#')؛
}
```

عند التنفيذ:

```shell, rtl
أ
ب
ج
د
! B _#
```

**أحرف التحكم**

أحرف التحكم هي رموز خاصة (تبدأ بالشَرطة المائلة العكسية **\\**) تُستخدم للتحكم في طريقة عرض البيانات أو لتنفيذ عمليات معينة دون ظهورها كجزء من النص، وهى:

| حرف التحكم | في لغة C | الوظيفة                                                           |
| :--------: | :------: | ----------------------------------------------------------------- |
|    \\"     |   \\"    | استخدام علامة التنصيص المزدوجة كحرف أصلي                          |
|    \\'     |   \\'    | استخدام علامة التنصيص المُفردة كحرف أصلي                          |
|    \\\\    |   \\\\   | استخدام علامة الشرطة المائلة العكسية كحرف أصلي                    |
|    \\س     |   \\n    | الانتقال إلى سطر جديد                                             |
|    \\ف     |   \\t    | التحرك مسافة أفقية                                                |
|    \\ر     |   \\v    | التحرك مسافة رأسية                                                |
|    \\خ     |   \\b    | التحرك مسافة للخلف (يقوم بمسح آخر حرف مثل زر الbackspace)         |
|    \\ص     |   \\f    | الانتقال إلى الصفحة التالية أثناء الكتابة (يظهر أثره في الطابعات) |
|    \\ج     |   \\r    | إرجاع المؤشر إلى بداية السطر، وبدء الكتابة منه                    |
|    \\0     |   \\0    | إنهاء النص                                                        |

إليك مثال لتوضيح وظيفة تلك الأحروف:

```nazm
دالة البداية() {
    اظهر("علامة تنصيص مزدوجة: ")؛
    اظهر_('\"')؛

    اظهر("علامة تنصيص مُفردة: ")؛
    اظهر_('\'')؛

    اظهر("شَرطة مائلة خلفية: ")؛
    اظهر_('\\')؛

    اظهر("سننتقل إلى سطر جديد باستخدام حرف التحكم بدلاً من اظهر_: ")؛
    اظهر('\س')؛

    اظهر("هذه مسافة")؛
    اظهر('\ف')؛
    اظهر_("أفقية")؛

    اظهر("وهذه مسافة")؛
    اظهر('\ر')؛
    اظهر_("رأسية")؛

    اظهر("--- وهنا سيرجع المؤشر إلى أول هذا السطر وسيضع فواصل بدل الشَرطات الموجودة في أول السطر: ")؛
    اظهر('\ج')؛
    اظهر_("،،،")

    اظهر("هنا ستظهر ألف واحدة بدل اثنان: أأ")؛
    اظهر('\خ')؛
}
```

عند التنفيذ:

```shell, rtl
علامة تنصيص مزدوجة: "
علامة تنصيص مُفردة: '
شَرطة مائلة خلفية: \
سننتقل إلى سطر جديد باستخدام حرف التحكم بدلاً من اظهر_:
هذه مسافة	أفقية
وهذه مسافة
          رأسية
،،، وهنا سيرجع المؤشر إلى أول هذا السطر وسيضع فواصل بدل الشَرطات الموجودة في أول السطر:
هنا ستظهر ألف واحدة بدل اثنان: أ
```

**ترميز يونيكود**

في `نظم`، يمكن استخدام ترميز يونيكود لتمثيل مجموعة واسعة من الأحرف من لغات مختلفة ورموز متنوعة. يُعتبر ترميز يونيكود طريقة موحدة لتمثيل النصوص بطريقة تضمن ظهور الأحرف بشكل صحيح عبر مختلف الأنظمة والأجهزة.

فقط قُم بكتابة علامة الشرطة المائلة الخلفية (`\\`) ثم حرف الياء (`ي`)، ثم ضع كود رمز اليونيكود المكون من أربع أرقام بالنظام السُداسي عشر:

```nazm
دالة البداية() {
   اظهر("هذا حرف الألف باليونيكود: ")؛
   اظهر_('\ي0627')؛

   اظهر("وهذا حرف التاء باليونيكود: ")؛
   اظهر_('\ي062A')؛

   اظهر("وهذا رمز باي باليونيكود: ")؛
   اظهر_('\ي03C0')؛
}
```

عند التنفيذ:

```shell, rtl
هذا حرف الألف باليونيكود: أ
وهذا حرف التاء باليونيكود: ت
وهذا رمز باي باليونيكود: π
```

> [!ملحوظة]
> بعض الرموز في اليونيكود التي تمثل أشياء تخالف الإسلام (صُلبان، موسيقى، شذوذ، إلخ) أو بعض الرموز الغير مدعومة، سيقوم المترجم بإظهار خطأ إذا تم اكتشاف أحد تلك الرموز في الكود (حتى لو كان مكتوباً بترميز اليونيكود).

**حجم الحرف**

الحروف في `نظم` تشغل حجم 4 بايت في الذاكرة، وتُمثِّل ما يُعرف **بقيمة يونيكود القياسية** للحرف.
هذا التصميم يسمح بتمثيل جميع الأحرف بما في ذلك تلك التي تحتاج إلى أكثر من 1 أو 2 بايت كما هو الحال في نظام UTF-8.
على الرغم من أن بعض الأحرف في UTF-8 قد تشغل أحجامًا مختلفة (مثل 1 بايت للأحرف الإنجليزية أو 2 بايت للأحرف العربية)،
فإن تخصيص 4 بايت لكل حرف في نظم يضمن التوافق الكامل مع جميع رموز يونيكود.

### المتون

في لغة `نظم`، المتون هي السلاسل النصية التي تُكتب بين علامتي تنصيص مزدوجتين (`"`). المتون تُستخدم لتمثيل النصوص المكونة من سلسلة من الأحرف، سواء كانت أحرفًا أبجدية، أرقامًا، رموزًا، أو حتى مسافات وأحرف تحكم.

```nazm
دالة البداية() {
    اظهر_("هذا متن")؛
}
```

بالطبع، قد استعملنا هذا النوع كثيرًا منذ البداية في هذا الباب، واسمه الكامل هو (**#متن**)، وحجمه يعتمد على عدد الحروف الموجودة بداخله وحجم كل حرف.
ولكن يوجد نوع آخر مشابه له تماماً، وهو النوع (**نص**)، وسنعرف الفرق بينهما و(كذلك معنى الرمز **#** في النوع **#متن**) في جزء الذاكرة إن شاء الله.

وأيضاً يمكنك استعمال أحرف التحكم وترميز يونيكود داخل المتون:

```nazm
دالة البداية() {
    اظهر_("هذا متن تحته رمز الألف\س:\ي0627")؛
}
```

عند التنفيذ:

```shell, rtl
هذا متن تحته رمز الألف:
أ
```

## الخاتمة

إليك جدول بأنواع البيانات الموجودة في لغة `نظم`:

| النوع  | الحجم (بالبايت) | النوع المُقابل في لغة C | التعريف                               |
| :----: | :-------------: | :---------------------: | ------------------------------------- |
|  `ط1`  |        1        |         uint8_t         | عدد طبيعي (8 بت)                      |
|  `ط2`  |        2        |        uint16_t         | عدد طبيعي (16 بت)                     |
|  `ط4`  |        4        |        uint32_t         | عدد طبيعي (32 بت)                     |
|  `ط8`  |        8        |        uint64_t         | عدد طبيعي (64 بت)                     |
|  `ص1`  |        1        |         int8_t          | عدد صحيح (8 بت)                       |
|  `ص2`  |        2        |         int16_t         | عدد صحيح (16 بت)                      |
|  `ص4`  |        4        |         int32_t         | عدد صحيح (32 بت)                      |
|  `ص8`  |        8        |         int64_t         | عدد صحيح (64 بت)                      |
|  `ع4`  |        4        |          float          | عدد عشري (32 بت)                      |
|  `ع8`  |        8        |         double          | عدد عشري مزدوج الدقة (64 بت)          |
| `شرط`  |        1        |   bool (C99 فيما بعد)   | يحتمل قيمتين (صحيح أو فاسد)           |
| `حرف`  |        4        |        char32_t         | حرف واحد                              |
| `#متن` | يعتمد على الطول |         char\*          | نص يمكن أن يحتوي على أي عدد من الأحرف |
